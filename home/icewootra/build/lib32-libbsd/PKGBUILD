# Maintainer: josephgbr <rafael.f.f1@gmail.com>

_pkgbase=libbsd
pkgname=lib32-$_pkgbase
pkgver=0.6.0
pkgrel=2
pkgdesc="Provides useful functions commonly found on BSD systems like strlcpy() (32 bit)"
arch=('x86_64')
url="http://libbsd.freedesktop.org"
license=('custom')
depends=('lib32-glibc' "$_pkgbase")
makedepends=('gcc-multilib')
options=(!libtool)
source=($url/releases/$_pkgbase-$pkgver.tar.xz)
md5sums=('f6c75f0a9818e323a589bcbd560a0eb4')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  
  cd $_pkgbase-$pkgver  
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd $_pkgbase-$pkgver
  make DESTDIR="$pkgdir" install
  rm -rf "$pkgdir"/usr/{include,share/man}
  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
